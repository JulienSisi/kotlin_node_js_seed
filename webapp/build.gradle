group 'com.techprd'
version '1.0.0'

apply plugin: 'kotlin2js'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:$html_version"
    compile project(':common')
}

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "${projectDir}/public/js/main.js"
    kotlinOptions.main = "call"
    kotlinOptions.moduleKind = "umd"
    // these two options enable browser debugging
    // disable for production to reduce file size
    kotlinOptions.sourceMap = true
    kotlinOptions.sourceMapEmbedSources = "always"
}

sourceSets {
    main.kotlin.srcDirs += "src"
}

task copyResources(type: Copy) {
    from sourceSets.main.resources.srcDirs
//    into sourceSets.main.output.resourcesDir
    into file(buildDir.path + "/public/js")
}

build.doLast {
    configurations.compile.each { File file ->
        copy {
            includeEmptyDirs = false

            from zipTree(file.absolutePath)
            into "${projectDir}/public/kotlin"
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
            }
        }
    }
}
